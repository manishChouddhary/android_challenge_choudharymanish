plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id "org.jmailen.kotlinter"
}

android {
    compileSdk 31

    defaultConfig {
        resConfigs 'en'
        applicationId "com.manish.carshopee"
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        def secureProps = new Properties()
        def securePropertiesFilePath = "../secure.properties"
        if (file(securePropertiesFilePath).exists()) {
            file(securePropertiesFilePath)?.withInputStream { secureProps.load(it) }
        }

        def network_key = "key"
        resValue "string", network_key, secureProps.getProperty(network_key)

        def base_url = "base_url"
        resValue "string", base_url, secureProps.getProperty(base_url)

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
        dataBinding true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'

    //dagger
    kapt 'com.google.dagger:dagger-compiler:2.40.5'
    kapt 'com.google.dagger:dagger-android-processor:2.40.5'
    implementation 'com.google.dagger:dagger-android:2.40.5'
    implementation 'com.google.dagger:dagger-android-support:2.40.5'

    //networking
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'
    implementation 'com.squareup.moshi:moshi:1.13.0'
    implementation 'com.squareup.moshi:moshi-kotlin:1.13.0'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.13.0'

    //rxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.2.9'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    //view model
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.0'
    //Activity
    implementation 'androidx.activity:activity-ktx:1.4.0'
    implementation 'androidx.fragment:fragment-ktx:1.4.1'

    //unit test
    testImplementation 'junit:junit:4.13.2'

    //instrumentation test
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}